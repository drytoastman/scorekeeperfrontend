plugins {
    id 'application'
    id 'eclipse'
    id "org.beryx.runtime" version "1.11.4"
    id "org.openjfx.javafxplugin" version "0.0.9"
}

def getVersionName = { ->
    try {
        def stdout = new ByteArrayOutputStream()
        exec {
            commandLine 'git', 'describe'
            standardOutput = stdout
        }
        def ret = stdout.toString().trim()
        if (ret.contains('-'))
            throw new Exception("not on tag")
        return ret
    } catch (Exception e) {
        return "latest"
    }
}

version = getVersionName()
mainClassName = 'org.wwscc.system.ScorekeeperSystem'
repositories {
    jcenter()
}

javafx {
    version = "13"
    modules = [
        'javafx.controls',
        'javafx.fxml',
        'javafx.media',
        'javafx.graphics',
        'javafx.swing',
        'javafx.web'
    ]
}

dependencies {
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.10.0'
    implementation 'com.github.jnr:jnr-unixsocket:0.23'
    implementation 'com.miglayout:miglayout-swing:5.2'
    implementation 'com.fazecast:jSerialComm:2.5.2'
    implementation 'com.jcraft:jsch:0.1.54'

    implementation 'io.swagger:swagger-annotations:1.5.18'
    implementation 'javax.annotation:javax.annotation-api:1.3.2'
    implementation 'org.apache.httpcomponents:httpclient:4.5.13'
    implementation 'org.kamranzafar:jtar:2.3'
    implementation 'org.mindrot:jbcrypt:0.4'
    implementation 'org.postgresql:postgresql:42.2.5'

    testImplementation 'junit:junit:4.12'
}

jar {
    manifest {
        attributes("Implementation-Version": version)
    }
}

def jmodules = [
    'java.base',
    'java.datatransfer',
    'java.desktop',
    'java.logging',
    'java.naming',
    'java.prefs',
    'java.scripting',
    'java.security.jgss',
    'java.security.sasl',
    'java.sql',
    'java.transaction.xa',
    'java.xml',
    'jdk.crypto.ec',
    'jdk.jfr',
    'jdk.jsobject',
    'jdk.unsupported',
    'jdk.unsupported.desktop',
    'jdk.xml.dom'
]

runtime {
    options = ['--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages']
    modules = jmodules

    targetPlatform("win") {
        jdkHome = jdkDownload("https://github.com/AdoptOpenJDK/openjdk15-binaries/releases/download/jdk-15.0.1%2B9/OpenJDK15U-jdk_x64_windows_hotspot_15.0.1_9.zip")
    }

    targetPlatform("linux") {
        jdkHome = jdkDownload("https://github.com/AdoptOpenJDK/openjdk15-binaries/releases/download/jdk-15.0.1%2B9/OpenJDK15U-jdk_x64_linux_hotspot_15.0.1_9.tar.gz")
    }

    targetPlatform("mac") {
        jdkHome = jdkDownload("https://github.com/AdoptOpenJDK/openjdk15-binaries/releases/download/jdk-15.0.1%2B9/OpenJDK15U-jdk_x64_mac_hotspot_15.0.1_9.tar.gz")
    }

    launcher {
        noConsole = true
    }
}


task distribzips {
    afterEvaluate { project ->
        dependsOn('runtime')
    }
    doLast {
        def dir = runtime.imageDir.get()
        def base = "$dir/$project.name"

        if (runtime.targetPlatforms.get()['win']) {
            def name = 'win'
            delete "$base-$name/release"
            new File("$base-$name/Scorekeeper.bat").text = """
                cd %~dp0bin
                start javaw.exe -classpath "../lib/*" org.wwscc.system.ScorekeeperSystem
            """
            new File("$base-$name/ProTimer.bat").text = """
                cd %~dp0bin
                start javaw.exe -classpath "../lib/*" org.wwscc.protimer.ProSoloInterface
            """
            new File("$base-$name/fwrules.bat").text = """
                %echo off
                CALL :addrule ScorekeeperWeb       TCP 80
                CALL :addrule ScorekeeperTimers    TCP 54328
                CALL :addrule ScorekeeperDatabase  TCP 54329
                CALL :addrule ScorekeeperDNS       UDP 53
                CALL :addrule ScorekeeperMDNS      UDP 5353
                CALL :addrule ScorekeeperDiscovery UDP 5454

                CALL :disableservice w3svc
                CALL :disableservice SharedAccess

                pause
                exit /B 0

                :addrule
                netsh advfirewall firewall show rule name=%1
                if %ERRORLEVEL% NEQ 0  netsh advfirewall firewall add rule name=%1 dir=in action=allow protocol=%2 localport=%3
                EXIT /B 0

                :disableservice
                sc query %1
                if %ERRORLEVEL% EQU 0 sc stop %1 & sc config %1 start=disabled
                EXIT /B 0
            """
            // file("$base-$name").renameTo(file("$base-$version-$name"))
            createZip(new File("$base-$name"), new File("$base-$version-$name" + ".zip"))
        }

        ['linux', 'mac'].each { name ->
            if (runtime.targetPlatforms.get()[name]) {
                delete "$base-$name/release"
                new File("$base-$name/Scorekeeper").text = """
                    #!/usr/bin/env bash
                    cd bin
                    ./java -classpath "../lib/*" org.wwscc.system.ScorekeeperSystem
                """
                // file("$base-$name").renameTo(file("$base-$version-$name"))
                createZip(new File("$base-$name"), new File("$base-$version-$name" + ".zip"))
            }
        }
    }
}

ext.createZip = { imageDir, zipFile ->
    def parentPath = imageDir.toPath()
    project.ant.zip(destfile: zipFile, duplicate: 'fail') {
        imageDir.eachFileRecurse { f ->
            int mode = f.canExecute() ? 755 : 644
            def relPath = parentPath.relativize(f.toPath()).toString()
            zipfileset(dir: parentPath, includes: relPath, filemode: mode)
        }
    }
}

/**************** Misc stuff ***************************/

task ver {
    doLast() { print "${version}" }
}

task depsize {
    doLast() {
        def size = 0
        def formatStr = "%,10.2f"
        configurations.default.collect { it.length() / (1024 * 1024) }.each { size += it }

        def out = new StringBuffer()
        out << 'Total dependencies size:'.padRight(45)
        out << "${String.format(formatStr, size)} Mb\n\n"
        configurations.default.sort { -it.length() } .each {
            out << "${it.name}".padRight(45)
            out << "${String.format(formatStr, (it.length() / 1024))} kb\n"
        }
        println out
    }
}
